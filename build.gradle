plugins {
	id 'maven-publish'
	alias libs.plugins.quilt.loom
}

def ENV = System.getenv()
def now = new Date()

version = ENV.TAG ?: "development-${now.format('YY.MMdd.HHmm', TimeZone.getTimeZone('UTC'))}+${libs.versions.minecraft.get()}${(!ENV.TAG && ENV.BUILD_NUMBER) ? "-build.${ENV.BUILD_NUMBER}" : ''}"
group = project.maven_group

// Minecraft 1.20.6 (24w14a) upwards uses Java 21.
def javaVersion = 21

base {
	archivesName.set("${rootProject.name}-fabric")
}

sourceSets {
	testmod {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

loom {
	mods {
		'libzoomer' {
			sourceSet('main')
		}
		'libzoomer_test' {
			sourceSet('testmod')
		}
	}

	mixin {
		useLegacyMixinAp = false
	}
}

repositories {
	maven {
		name = 'ParchmentMC'
		url = 'https://maven.parchmentmc.org'
		content {
			includeGroupByRegex 'org\\.parchmentmc(\\..*)?'
		}
	}
}

// All the dependencies are declared at gradle/libs.version.toml and referenced with "libs.<id>"
// See https://docs.gradle.org/current/userguide/platforms.html for information on how version catalogs work.
dependencies {
	minecraft libs.minecraft
	mappings loom.officialMojangMappings()
	/*
	mappings(loom.layered {
		it.parchment libs.parchment
		it.officialMojangMappings()
	})
	*/
	modImplementation libs.quilt.loader

	// Ughhhhhhhhhhh
	modImplementation libs.fabric.api

	afterEvaluate {
		testmodImplementation sourceSets.main.output
	}
}

loom {
	runs {
		testmodClient {
			client()
			configName = 'Testmod Client'
			source(project.sourceSets.testmod)
		}
	}
}

processResources {
	def expandProps = [
	    'version' : version,
		'maven_group': project.maven_group
	]

	inputs.properties(expandProps)

	filesMatching(['fabric.mod.json', 'quilt.mod.json']) {
		expand expandProps
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release.set(javaVersion)
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	// If this mod is going to be a library, then it should also generate Javadocs in order to aid with development.
	// Uncomment this line to generate them.
	withJavadocJar()

	toolchain {
		languageVersion.set(JavaLanguageVersion.of(javaVersion))
	}
}

jar {
	from('LICENSE*') {
		rename { "LICENSE_${rootProject.name}" }
	}
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifactId = "${rootProject.name}-fabric"

			pom {
				name = 'LibZoomer'
				description = 'A library that allows other Minecraft mods to have powerful zooming easily.'
				url = 'https://github.com/Up-Mods/LibZoomer'
				licenses {
					license {
						name = 'The MIT License'
						url = 'https://opensource.org/licenses/MIT'
					}
				}
				developers {
					developer {
						id = 'EnnuiL'
						name = 'Ennui Langeweile'
					}
				}
				scm {
					connection = 'scm:git:https://github.com/Up-Mods/LibZoomer.git'
					developerConnection = 'scm:git:git@github.com:Up-Mods/LibZoomer.git'
					url = 'https://github.com/Up-Mods/LibZoomer'
				}
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
		if (ENV.MAVEN_UPLOAD_URL) {
			maven {
				url = ENV.MAVEN_UPLOAD_URL
				credentials {
					username = ENV.MAVEN_UPLOAD_USERNAME
					password = ENV.MAVEN_UPLOAD_PASSWORD
				}
			}
		}
	}
}
